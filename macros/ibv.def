# ref: https://github.com/linux-rdma/rdma-core:libibverbs/verbs.h
subcode: _autoload
    $include infiniband/verbs.h
    $uselib ibverbs

macros:
    ctx:g_ibv_ctx
    pd: g_ibv_pd
    mr_buf: g_ibv_mr_buf
    mr: g_ibv_mr
    compchan: g_ibv_chan
    cq: g_ibv_cq
    qp: g_ibv_qp

#---------------------------------------- 
subcode: ibv_init
    $call ibv_open, 0
    $call ibv_alloc_pd
    $global char g_ibv_mr_buf[1024]
    $call ibv_reg_mr, g_ibv_mr_buf, 1024
    $call ibv_create_comp_channel
    $call ibv_create_cq
    $call ibv_create_qp

    $struct(QPInfo) uint16_t lid, uint32_t qp_number
    $local struct QPInfo local_qp_info
    $local struct QPInfo remote_qp_info
    &call ibv_query_port, 1
        local_qp_info.lid = $(attr).lid
        local_qp_info.qp_number = $(qp)->qp_num


subcode: ibv_finish
    $call ibv_destroy_qp
    $call ibv_destroy_cq
    $call ibv_destroy_comp_channel
    $call ibv_dereg_mr
    $call ibv_dealloc_pd
    $call ibv_close

#---------------------------------------- 
subcode: ibv_open(i_device)
    $local struct ibv_device ** device_list, n_device_list
    device_list = ibv_get_device_list(&n_device_list)
    $if !device_list
        $call die, "Error: ibv_get_device_list() failed"

    $if n_device_list<$(i_device)
        $call die, "ibv device #$(i_device) not exist"

    $global struct ibv_context * $(ctx)
    $(ctx) = ibv_open_device(device_list[$(i_device)])
    $if !$(ctx)
        $call die, "Failed to open ibv device #$(i_device)"

    # $print ibv_open: opened device #$(i_device).
    $call @query_device_list
    ibv_free_device_list(device_list)

subcode: ibv_close
    ibv_close_device($(ctx))

# -------------------
subcode: ibv_alloc_pd
    $global struct ibv_pd * $(pd)
    $(pd) = ibv_alloc_pd($(ctx))
    $if !$(pd)
        $call die, "Failed to allocate $(pd)"

subcode: ibv_dealloc_pd
    ibv_dealloc_pd($(pd))

# -------------------
subcode: ibv_reg_mr(p, size)
    $local struct ibv_mr * $(mr)
    $(set:flag=IBV_ACCESS_LOCAL_WRITE)
    $(mr) = ibv_reg_mr($(pd), $(p), $(size), $(flag))
    $if !$(mr)
        $call die, "Failed: ibv_reg_mr"

subcode: ibv_dereg_mr
    ibv_dereg_mr($(mr))

# -------------------
subcode: ibv_create_comp_channel
    $local struct ibv_comp_channel * $(compchan)
    $(compchan) = ibv_create_comp_channel($(ctx))
    $if !$(compchan)
        $call die, "Failed: ibv_create_comp_channel"

subcode: ibv_destroy_comp_channel
    ibv_destroy_comp_channel($(compchan))

# -------------------
subcode: ibv_create_cq
    $local struct ibv_cq * $(cq)
    $(cq) = ibv_create_cq($(ctx), 1024, NULL, $(compchan), 0)
    $if !$(cq)
        $call die, "Failed: ibv_create_cq"

subcode: ibv_destroy_cq
    ibv_destroy_cq($(cq))

# -------------------
subcode: ibv_create_qp
    $local struct ibv_qp * $(qp)
    struct ibv_qp_init_attr init_attr = {
        .send_cq = $(cq),
        .recv_cq = $(cq),
        .cap = {
            .max_send_wr = 1,
            .max_recv_wr = 1, 
            .max_send_sge = 1,
            .max_recv_sge = 1,
        },
        .qp_type = IBV_QPT_RC, # RC, UC, UD
        .sq_sig_all = 0, # decide by work request
    };

    $(qp) = ibv_create_qp($(pd), &init_attr)
    $if !$(qp)
        $call die, "Failed: ibv_create_qp"

subcode: ibv_destroy_qp
    ibv_destroy_qp($(qp))

# -------------------
subcode: modify_qp(state)
    $local struct ibv_qp_attr qp_attr
    BLOCK
    tn_ret = ibv_modify_qp($(qp), &qp_attr, $(mask))
    $if tn_ret
        $call die, "Failed to modify_qp to state $(state)"

subcode: modify_qp_init
    &call modify_qp, init
        qp_attr = (struct ibv_qp_attr){
            .qp_state = IBV_QPS_INIT,
            .pkey_index = 0,
            .port_num = 1,
            .qp_access_flags = $(join:IBV_ACCESS_*: | :LOCAL_WRITE,REMOTE_READ,REMOTE_ATOMIC,REMOTE_WRITE),
        };
        $(export:mask=IBV_QP_STATE | IBV_QP_PKEY_INDEX | IBV_QP_PORT | IBV_QP_ACCESS_FLAGS)

subcode: modify_qp_rtr
    &call modify_qp, rtr
        qp_attr = (struct ibv_qp_attr){
            .qp_state = IBV_QPS_RTR,
            .path_mtu = IBV_MTU_4096,
            .dest_qp_num = remote_qp_info.qp_number,
            .rq_psn = 0,
            .max_dest_rd_atomic = 1,
            .min_rnr_timer = 12,
            .ah_attr = {
                .is_global = 0,
                .dlid = remote_qp_info.lid,
                .sl = 0,
                .src_path_bits = 0,
                .port_num = 1 },
        };
        $(export:mask=IBV_QP_STATE|IBV_QP_PATH_MTU|IBV_QP_DEST_QPN|IBV_QP_RQ_PSN|IBV_QP_MAX_DEST_RD_ATOMIC|IBV_QP_MIN_RNR_TIMER|IBV_QP_AV)

subcode: modify_qp_rts
    &call modify_qp, rts
        qp_attr = (struct ibv_qp_attr){
            .qp_state = IBV_QPS_RTS,
            .timeout = 14,
            .retry_cnt = 7,
            .rnr_retry = 7,
            .sq_psn = 0,
            .max_rd_atomic = 1,
        };
        $(export:mask=IBV_QP_STATE|IBV_QP_TIMEOUT|IBV_QP_RETRY_CNT|IBV_QP_RNR_RETRY|IBV_QP_SQ_PSN|IBV_QP_MAX_QP_RD_ATOMIC)

# -------------------
subcode: ibv_query_port(i_port)
    $(set:attr=ibv_port_attr)
    $local struct ibv_port_attr $(attr)
    ibv_query_port($(ctx), $(i_port), &$(attr))
    BLOCK

# -------------------------------- 
subcode: ibv_post_wr(buf, size, send)
    struct ibv_sge sg_list = {
        .addr = (uint64_t)$(buf),
        .length = $(size),
        .lkey = $(mr)->lkey,
    };
    struct ibv_$(send)_wr wr = {
        .next = NULL,
        .sg_list = &sg_list,
        .num_sge = 1,
        $(if:send=send)
            .opcode = IBV_WR_SEND,
            .send_flags = IBV_SEND_SIGNALED,
    };
    $my struct ibv_$(send)_wr *p_bad_wr
    tn_ret = ibv_post_$(send)($(qp), &wr, &p_bad_wr)
    $if tn_ret
        $call die, "Error: ibv_post_$(send)"

subcode: ibv_send(buf, size)
    $call ibv_post_wr, $(buf), $(size), send

subcode: ibv_recv(buf, size)
    $call ibv_post_wr, $(buf), $(size), recv

# -------------------------------- 
subcode: ibv_poll_cq
    $my struct ibv_wc wc
    $my int tn_comp = 0
    $while tn_comp == 0
        tn_comp = ibv_poll_cq($(cq), 1, &wc)
        $if tn_comp>0
            $print "[$mpi_id] got wc: qp_num=%d", wc.qp_num
    $if tn_comp < 0
        $call die, "Failed: ibv_poll_cq"
    $if wc.status != IBV_WC_SUCCESS
        $call die, "[$mpi_id] Error: ibv_poll_cq return failure"

#---------------------------------------- 
subcode: query_ibv_devices
    $call ibv_open, 0
    $call @query_ctx
    &call @query_port, 1
        $print "  Port $(i_port)"
        $(for:lid, gid_tbl_len, pkey_tbl_len, max_mtu)
            $print "    $1: %d", ibv_port_attr.$1
    $call @query_gid
    $call @query_pkey
    $call ibv_close

    subcode: query_device_list
        $print $n_device_list ibv devices
        $for i=0:n_device_list
            $print "  $i: ibv device [%s] - %s", ibv_get_device_name(device_list[i]), ibv_node_type_str(device_list[i]->node_type)

    subcode: query_ctx
        $print "num_comp_vectors: %d", $(ctx)->num_comp_vectors
        $print "async_fd: %d", $(ctx)->async_fd

        $local struct ibv_device_attr ibv_device_attr
        ibv_query_device($(ctx), &ibv_device_attr)
        $print "  Node GUID: %p", ibv_device_attr.node_guid
        $(for:max_qp, max_qp_wr, max_mr_size, max_pkeys, phys_port_cnt)
            $print "    $1: %d", ibv_device_attr.$1
        
    subcode: query_pkey
        $local uint16_t pkey
        $for i=0:ibv_port_attr.pkey_tbl_len
            ibv_query_pkey($(ctx), 1, i, &pkey)
            $if pkey > 0
                $print "pkey #$i: %x", pkey

    subcode: query_gid
        $local union ibv_gid gid
        $for i=0:ibv_port_attr.gid_tbl_len
            int _ret = ibv_query_gid($(ctx), 1, i, &gid);
            $if _ret==0
                $print "gid #$i: $(x16:%02x)", $(join:gid.raw[*]:,:0-15)
